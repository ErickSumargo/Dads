def plugins = [
    "com.android.library",
    "kotlin-android",
    "kotlin-kapt",
    "dagger.hilt.android.plugin"
]

plugins.forEach { plugin ->
    apply plugin: plugin
}

apply from: rootProject.file("plugins/jacoco.gradle")

hilt {
    enableExperimentalClasspathAggregation = true
}

kapt {
    correctErrorTypes = true
}

android {
    compileSdkVersion sdk.compile

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    defaultConfig {
        minSdkVersion sdk.min
        targetSdkVersion sdk.target

        consumerProguardFiles "proguard-rules.pro"
        testInstrumentationRunner "${rootProject.applicationId}.lib.instrumentation.runner.HiltTestRunner"
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = [
            "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xopt-in=kotlinx.coroutines.FlowPreview",
            "-Xopt-in=kotlinx.coroutines.InternalCoroutinesApi",
            "-Xopt-in=kotlin.time.ExperimentalTime"
        ]

        jvmTarget = JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false

        error("VisibleForTests")
    }

    packagingOptions {
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
        exclude "META-INF/*.kotlin_module"
    }
}

dependencies {
    // annotation
    implementation project(":annotation")

    // dagger
    implementation "com.google.dagger:hilt-android:$deps.dagger"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$deps.dagger"

    kapt "com.google.dagger:hilt-android-compiler:$deps.dagger"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$deps.dagger"

    // fragment
    implementation "androidx.fragment:fragment-ktx:$deps.fragment"

    // hilt
    implementation "androidx.hilt:hilt-navigation-fragment:$deps.hilt"
    implementation "androidx.hilt:hilt-work:$deps.hilt"

    kapt "androidx.hilt:hilt-compiler:$deps.hilt"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$deps.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$deps.navigation"

    // processor
    kapt project(":processor")

    // truth
    androidTestImplementation "com.google.truth:truth:$deps.truth"
}

dependencies {
    // libs
    implementation project(":lib_navigation")
    implementation project(":lib_presentation")
    implementation project(":lib_threading")

    androidTestImplementation project(":lib_instrumentation")
    androidTestImplementation project(":lib_threading_test")
}
