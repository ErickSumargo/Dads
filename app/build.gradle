def plugins = [
    "com.android.application",
    "com.google.gms.google-services",
    "com.google.firebase.crashlytics",
    "kotlin-android",
    "kotlin-kapt",
    "dagger.hilt.android.plugin"
]

plugins.forEach { plugin ->
    apply plugin: plugin
}

android {
    compileSdkVersion sdk.compile

    defaultConfig {
        applicationId rootProject.applicationId
        minSdkVersion sdk.min
        targetSdkVersion sdk.target
        versionCode sdk.code
        versionName sdk.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            debuggable false

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
    }

    packagingOptions {
        exclude "META-INF/AL2.0"
        exclude "META-INF/LGPL2.1"
    }
}

dependencies {
    // dagger
    implementation "com.google.dagger:hilt-android:$deps.dagger"

    kapt "com.google.dagger:hilt-android-compiler:$deps.dagger"

    // firebase
    implementation platform("com.google.firebase:firebase-bom:$deps.bom")

    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"

    // graphql
    implementation "com.apollographql.apollo:apollo-runtime:$deps.graphql"
    implementation "com.apollographql.apollo:apollo-coroutines-support:$deps.graphql"

    // hilt
    implementation "androidx.hilt:hilt-navigation-fragment:$deps.hilt"
    implementation "androidx.hilt:hilt-work:$deps.hilt"

    kapt "androidx.hilt:hilt-compiler:$deps.hilt"

    // leakcanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$deps.leakcanary"

    // navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$deps.navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$deps.navigation"

    // okhttp
    implementation "com.squareup.okhttp3:logging-interceptor:$deps.okhttp"

    // work
    implementation "androidx.work:work-runtime-ktx:$deps.work"
}

dependencies {
    // features
    implementation project(":feature_home")

    // domains
    implementation project(":domain_common")
    implementation project(":domain_home")

    // libs
    implementation project(":lib_database")
    implementation project(":lib_navigation")
    implementation project(":lib_preference")
    implementation project(":lib_presentation")
    implementation project(":lib_remote")
    implementation project(":lib_threading")
    implementation project(":lib_worker")
}
