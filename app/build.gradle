def plugins = [
    "com.android.application",
    "com.google.gms.google-services",
    "com.google.firebase.crashlytics",
    "kotlin-android",
    "kotlin-kapt",
    "dagger.hilt.android.plugin"
]

plugins.forEach { plugin ->
    apply plugin: plugin
}

android {
    compileSdkVersion sdk.compile

    defaultConfig {
        applicationId "com.bael.dads"
        minSdkVersion sdk.min
        targetSdkVersion sdk.target
        versionCode sdk.code
        versionName sdk.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"

            minifyEnabled false
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

def deps = [
    "dagger",
    "firebase",
    "graphql",
    "hilt",
    "leakcanary",
    "navigation",
    "okhttp",
    "work"
]

deps.forEach { dep ->
    apply from: "$rootDir/deps/${dep}.gradle"
}

def modules = [
    "feature_home",
    "lib_api",
    "lib_data",
    "lib_database",
    "lib_domain",
    "lib_preference",
    "lib_presentation",
    "lib_worker"
]

dependencies {
    modules.forEach { module ->
        implementation project(":$module")
    }
}
